#! /bin/csh -f
# $Id: configure,v 1.1 1994/01/20 13:33:29 joke Rel $
#
set default=--a4
set papersize=$default
set configdir=./config
set perldir=/usr/local/bin
set fontfamily=cm
set fontdir=./fonts
set srcdir=.
set package="EFF's (Extended) Guide to the Internet"
set edition="2.3"
#
set cp="ln -s"
set cp="cp"
#
top:
if ($#argv > 0) then
    switch ($1:q)

    case --l*:
    case --a*:
	set papersize = "$1:q"
	shift
	goto top

    case --f*:
	set fontfamily = "$2:q"
	set fontfile = "$fontdir/ps.$fontfamily"
	shift
	shift
	goto top

    case --p*:
	set perldir = "$2:q"
	shift
	shift
	goto top

    default:
	goto usage
    endsw
endif
#
rm -f $srcdir/Makefile
rm -f $srcdir/eegtti.texi
rm -f $fontdir/ps.fonts
#
if ("$fontfamily" != "cm") then
    $cp $fontfile $fontdir/ps.fonts
    cpp -I$fontdir -DPOSTSCRIPT -P fonts/texinfo.foo |uniq >texinfo.tex
else
    cpp -I$fontdir -P fonts/texinfo.foo |uniq >texinfo.tex
endif
#
if ("$papersize" == "--letter") then
    $cp $configdir/eegtti.us $srcdir/eegtti.texi
    $cp $configdir/Makefile.us $srcdir/Makefile
    echo "$package [Texinfo edition $edition, $fontfamily font] \
*** configured for US letter size paper."
    goto done
endif
#
if ("$papersize" == "--a4") then
    $cp $configdir/eegtti.a4 $srcdir/eegtti.texi
    $cp $configdir/Makefile.a4 $srcdir/Makefile
    echo "$package [Texinfo edition $edition, $fontfamily font] \
*** configured for A4 paper size."
    goto done
endif
#
usage:
    echo "usage: configure --<paper-size> --font <family> --perl <path>"
    echo "       --a4/--letter for <paper-size> (a4)"
    echo "       --font times/palatino/bookman/etc. for <family> (cm)"
    echo "       --perl <path> (/usr/local/bin)"
    echo ""
    echo "e.g.:  configure --a4 --font palatino --perl /usr/gnu/bin"
#
done:
